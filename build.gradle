plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.1'
	id 'io.spring.dependency-management' version '1.1.7'

	id 'com.google.protobuf' version '0.9.5'
}

ext {
	springGrpcVersion = "1.0.1"
}

group = 'flipnote'
version = '0.0.1-SNAPSHOT'
description = 'Group'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.grpc:spring-grpc-dependencies:$springGrpcVersion"
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'


	//Spring gRPC Starter
	implementation 'org.springframework.grpc:spring-grpc-spring-boot-starter'
	implementation 'com.google.protobuf:protobuf-java'
	implementation 'io.grpc:grpc-stub'

	implementation 'org.springframework.boot:spring-boot-starter-webmvc'

	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-data-redis-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testRuntimeOnly 'com.h2database:h2'
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"
}
/**
 * proto 컴파일 설정
 * - .proto 위치: src/main/proto
 * - 생성 위치: build/generated/...
 */
protobuf {
	protoc { artifact = "com.google.protobuf:protoc:4.33.2" }
	plugins {
		grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.77.1" }
	}
	generateProtoTasks {
		all().each { task ->
			task.plugins { grpc {} }
		}
	}
}

def querydslDir = layout.buildDirectory.dir("generated/querydsl").get().asFile
def protoGeneratedDir = layout.buildDirectory.dir("generated/source/proto/main").get().asFile

sourceSets {
	main {
		proto { srcDir "src/main/proto" }
		java {
			srcDirs querydslDir,
					"$protoGeneratedDir/java",
					"$protoGeneratedDir/grpc"
		}
	}
}

tasks.named('compileJava', JavaCompile) {
	options.generatedSourceOutputDirectory.set(querydslDir)
}

// compileJava가 generateProto 이후에 실행되도록 보장
tasks.named('compileJava') {
	dependsOn tasks.named('generateProto')
}
